--scope = outside

$DirtPile
  name = Mud
  verbLookAt:
    -> lookat_dirt
  verbPickUp:
    notHad(dirt) ? give(dirt) : -> enoughdirtthanks

:enoughdirtthanks
Steggy: Once was enough, I think.

$OutsideDoor
  name = Entrance to the Wedding Venue
  default = open
  @open = YES
  verbPull:
    @open == YES ? @open = NO : -> generics.its_already_closed
  verbPush:
    -> goinside
  verbOpen:
    -> goinside
  verbClose:
    @open == YES ? @open = NO : -> generics.its_already_closed
  verbLookAt:
    @open == YES ? -> its_open : -> its_closed
  verbUse:
    -> goinside

:goinside
@OutsideDoor.open = YES
goToRoom(reception, StartLeft)

*BrideA
  name = Maybe Bertrella?
  @dirty = NO
  verbLookAt:
    !done_the_intro == YES ? -> lookat_bride_a : -> express_surprise
  verbGive:
    with(beer) ? -> givebeer : -> dont_want_that
  verbUse:
    with(confetti) ? -> dirtify
    with(beer) ? -> givebeer
    with(tears) ? -> saltify
    with(dirt) ? -> too_clumpy
    with(asbestos) ? -> too_fine
    -> already_furious
  verbTalkTo:
    !done_the_intro == YES ? -> main : -> intro

*BrideB
  name = Bertrella, Maybe?
  @dirty = NO
  verbLookAt:
    !done_the_intro == YES ? -> lookat_bride_b : -> express_surprise
  verbGive:
    with(beer) ? -> givebeer : -> dont_want_that
  verbUse:
    with(confetti) ? -> dirtify
    with(beer) ? -> givebeer
    with(tears) ? -> saltify
    with(dirt) ? -> too_clumpy
    with(asbestos) ? -> too_fine
  verbTalkTo:
    !done_the_intro == YES ? -> main : -> intro

+TriggerIntro
  -> gameintro

+TriggerFinale
  !wet_and_salty == YES
  !covered_in_dirt == YES
  !beer_given == YES
    -> finale

:gameintro
!game_intro_done = YES
Steggy: Ah, what a lovely day for a wedding!
Steggy: I hope nothing goes wrong!

:express_surprise
Steggy: Hey that's weird!
Steggy: There appears to be two Bertrellas!

:lookat_bride_a
Steggy: She's either Bertrella or a Slug Monster. Hmm.

:lookat_bride_b
Steggy: She's either a Slug Monster or Bertrella. Hmm.

:lookat_dirt
Steggy: It's... sticky mud. Lovely, lovely mud.

:dont_want_that
Steggy: I don't think either of them want that.

:too_clumpy
Steggy: It's good and sticky but it's too clumpy.
Steggy: I wonder if there's something I could mix it with?

:too_fine
Steggy: It'll just blow away in the wind.
Steggy: I wonder if there's something I could mix it with?

:already_furious
Steggy: I'd better not.
Steggy: They're already furious with me.

:its_open
Steggy: It's an open door!

:its_closed
Steggy: It's a closed door!

:intro
!done_the_intro = YES
BrideA: Steggy! You have to help me!
Steggy: Ey up, what's the matter?
BrideB: Get away from her, Steggy!
BrideB: That's not me! That's...
BrideB: THAT'S A SLUG MONSTER!
Steggy: What the actual...
BrideA: No! She's the slug monster!
BrideB: You're the slug monster!
BrideA: You're ruining my wedding!!
BrideB: You're ruining MY wedding!!!
BrideA: NO YOU!
-> main

:main
1  Okay. Which one of you is the real Bertrella? -> menome [once]
2  A Slug Monster says what? -> what [once]
3  Tell me something only the real Bertrella would know. -> knowledge [once]
3  Tell me something you can't find on Twitter. -> knowledge [once]
3  Tell me something you can't find on Google. -> knowledge [once]
4  This is classic you, Bertrella. -> classic [once]
5  I guess muggins here needs to get this sorted out. -> done

:menome
BrideA: Me!
BrideB: No! Me!
BrideA: ME!
BrideB: LIAR!
Steggy: I had a feeling it wouldn't be that easy.
-> main

:what
BrideB: What?
BrideA: What?
-> main

:knowledge
!asked_for_knowledge++
BrideA:  I know you were once sick in my handbag. [!asked_for_knowledge == 1]
BrideB:  I know you blamed it on Noncy Paul! [!asked_for_knowledge == 1]
Steggy:  It WAS Noncy Paul! [!asked_for_knowledge == 1]
BrideB:  I know you're afraid you're being watched by a duck! [!asked_for_knowledge == 2]
BrideA:  I know the duck is called Bernard! [!asked_for_knowledge == 2]
Steggy:  Bernard? My brother Bernard? Is a duck? [!asked_for_knowledge == 2]
BrideA:  I know your birthday is in June [!asked_for_knowledge == 3]
Steggy:  July. [!asked_for_knowledge == 3]
BrideB:  I know you're 30 this year [!asked_for_knowledge == 3]
Steggy:  26. And thanks for that. [!asked_for_knowledge == 3]
Steggy:  Sigh. This is no use. [!asked_for_knowledge == 3]
-> main

:classic
BrideA: This has only happened once before, Steggy.
BrideB: Yeah I mean not like this ALWAYS happens.
Steggy: Okay, okay. Whatever.
-> main

:givebeer
BrideA: Sowerby Pumper! Ooo lovely!
BrideB: Give me some!
BrideA: Me first!
BrideB: NO ME!
Steggy: Hmmm. Results inconclusive.
destroy(beer)
!beer_given = YES

:dirtify
Steggy: Ey up, look at this!
Steggy: It's sticky brown confetti!
Steggy: Try not to breathe it in though...
@BrideA.dirty = YES
@BrideB.dirty = YES
Steggy: Oops.
BrideA: What is wrong with you, Steggy?
Steggy: I needed to know which one of you was the stickiest!
BrideB: Her! She's the stickiest!
BrideA: NO! IT'S HER!
Steggy: Bloody hell.
destroy(confetti)
!covered_in_dirt = YES

:saltify
Steggy: This HAS to work...
BrideA: You sprayed me!
BrideB: Me too!
Steggy: Either of you feeling a bit... melty?
BrideA: Melty? No.
BrideB: Angry? Yes.
BrideA: She's melting though! Look!
BrideB: I'm not! You are! Look!
Steggy: OH COME ON.
replace(tears, emptybottle)
!wet_and_salty = YES

:done
BrideB: Hurry! Everyone's waiting for me!
BrideA: They're waiting for me!
BrideB: No me!
BrideA: NO ME!
Steggy: *Sigh*

:finale
pause(0.5)
Steggy: Well, that's all three tests.
Steggy: I still have no idea which one of you is which.
Steggy: So... what now?
Groom: {teleportTo(GroomSpawn)}
Groom: {goTo(GroomMark)}
Groom: Steggy! Help!
Steggy: {goTo(TalkToGroom)}
Steggy: What's happened!
Groom: I've been cloned by a Slug Monster!
GroomClone: {teleportTo(GroomSpawn)}
GroomClone: {goTo(OtherGroomMark)}
GroomClone: Steggy! Help!
pause(1)
Steggy: You know what?
GroomThree: {teleportTo(GroomSpawn)}
GroomThree: {goTo(YetOtherGroomMark)}
GroomThree: Steggy! Help!
Steggy: {goTo(SteggyExitMark)}
Steggy: No that's just ridiculous now.
Steggy: SORT IT OUT YOURSELVES.
runCredits()
